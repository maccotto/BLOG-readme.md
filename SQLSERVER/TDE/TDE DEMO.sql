/*
 TDE: TRANSPARENT DATA ENCRYPTION
*/

-- CREAMOS LA MASTERKEY A NIVEL SERVIDOR

USE MASTER;
GO
CREATE MASTER KEY ENCRYPTION
BY PASSWORD='TRIGGERDB123.....';
GO

-- CREAMOS UN CERTIFICADO DESDE EL MISMO MSSQL
CREATE CERTIFICATE TDE_TRIGGERDBCERT
WITH 
SUBJECT='TRIGGERDB DATABASE ENCRYPTION';
GO

-- HACEMOS UN BACKUP DEL CERTIFICADO
BACKUP CERTIFICATE TDE_TRIGGERDBCERT
TO FILE = '/VAR/OPT/MSSQL/SHARE/TRIGGERDB_TDE_CERT'
WITH PRIVATE KEY (FILE='/VAR/OPT/MSSQL/SHARE/TRIGGERDB_TDE_CERTKEY.PVK',
ENCRYPTION BY PASSWORD='TRIGGERDB123456.....') 


-- CREAMOS EL DATABASE ENCRYPTION SOBRE LA BASE
USE AdventureWorks2019 
GO

CREATE DATABASE ENCRYPTION KEY
WITH ALGORITHM = AES_256 -- ALGORITMO
ENCRYPTION BY SERVER CERTIFICATE TDE_TRIGGERDBCERT;

-- VERIFICAMOS EL ESTADO DE LAS BASES DE DATOS
SELECT NAME,IS_ENCRYPTED  
FROM SYS.DATABASES 
WHERE NAME = 'ADVENTUREWORKS2019'

-- ENCENDEMOS EL TDE SOBRE LA BASE

ALTER DATABASE AdventureWorks2019
SET ENCRYPTION ON;
GO

-- VERIFICAMOS EL ESTADO DE LAS BASES DE DATOS
SELECT NAME,IS_ENCRYPTED  
FROM SYS.DATABASES 
WHERE NAME = 'ADVENTUREWORKS2019'

-- MONITOREO DEL AVANCE DE LA ENCRIPTACION
SELECT DB_NAME(DATABASE_ID) AS DATABASENAME, ENCRYPTION_STATE,
ENCRYPTION_STATE_DESC =
CASE ENCRYPTION_STATE
         WHEN '0'  THEN  'NO DATABASE ENCRYPTION KEY PRESENT, NO ENCRYPTION'
         WHEN '1'  THEN  'UNENCRYPTED'
         WHEN '2'  THEN  'ENCRYPTION IN PROGRESS'
         WHEN '3'  THEN  'ENCRYPTED'
         WHEN '4'  THEN  'KEY CHANGE IN PROGRESS'
         WHEN '5'  THEN  'DECRYPTION IN PROGRESS'
         WHEN '6'  THEN  'PROTECTION CHANGE IN PROGRESS (THE CERTIFICATE OR ASYMMETRIC KEY THAT IS ENCRYPTING THE DATABASE ENCRYPTION KEY IS BEING CHANGED.)'
         ELSE 'NO STATUS'
         END,
PERCENT_COMPLETE,ENCRYPTOR_THUMBPRINT, ENCRYPTOR_TYPE  
FROM SYS.DM_DATABASE_ENCRYPTION_KEYS
WHERE database_id = DB_ID()

--- HACEMOS BACKUP DE NUESTRA BASE ENCRIPTADA
BACKUP DATABASE [AdventureWorks2019] 
TO  DISK = N'/VAR/OPT/MSSQL/SHARE/AdventureWorks2019_TDE.bak' 
WITH NOFORMAT, INIT,  NAME = N'AdventureWorks2019 CON TDE', 
SKIP, NOREWIND, NOUNLOAD,  STATS = 10
GO

-----------------------------------------------------------
--------------------- SERVER SECUNDARIO ------------------- 
------------------------------------------------------------

-- probamos de hacer el restore

USE [master]
RESTORE DATABASE 
[AdventureWorks2019] 
FROM  DISK = N'/var/opt/mssql/compartir/AdventureWorks2019_TDE.bak' 
WITH  FILE = 1,  MOVE N'AdventureWorks2017' 
TO N'/var/opt/mssql/data/AdventureWorks2019.mdf',  
MOVE N'AdventureWorks2017_log' 
TO N'/var/opt/mssql/data/AdventureWorks2019_log.ldf',  
NOUNLOAD,  STATS = 5

-- CREAMOS LA MASTER KEY

USE MASTER;
GO
CREATE MASTER KEY ENCRYPTION
BY PASSWORD='TRIGGERDB123.....';
GO

-- CREAMOS EL CERTIFICADO USANDO EL BACKUP

CREATE CERTIFICATE TDE_TRIGGERDBCERT
FROM FILE = '/VAR/OPT/MSSQL/compartir/TRIGGERDB_TDE_CERT'
WITH PRIVATE KEY (FILE = '/VAR/OPT/MSSQL/compartir/TRIGGERDB_TDE_CERTKEY.PVK',
	DECRYPTION BY PASSWORD = 'TRIGGERDB123456.....');
GO

-- PROBAMOS AHORA EL RESTORE

USE [master]
RESTORE DATABASE 
[AdventureWorks2019] 
FROM  DISK = N'/var/opt/mssql/compartir/AdventureWorks2019_TDE.bak' 
WITH  FILE = 1,  MOVE N'AdventureWorks2017' 
TO N'/var/opt/mssql/data/AdventureWorks2019.mdf',  
MOVE N'AdventureWorks2017_log' 
TO N'/var/opt/mssql/data/AdventureWorks2019_log.ldf',  
NOUNLOAD,  STATS = 5

-- VERIFICAMOS EL ESTADO DE LAS BASES DE DATOS
SELECT NAME,IS_ENCRYPTED  
FROM SYS.DATABASES 
WHERE NAME = 'ADVENTUREWORKS2019'

------------------------------------------------
---- BACKUP COMPRESSION EN TDE -----------------
------------------------------------------------

BACKUP DATABASE [AdventureWorks2019] 
TO  DISK = N'/VAR/OPT/MSSQL/SHARE/AdventureWorks2019_TDE_COMPRESS1.bak' 
WITH NOFORMAT, INIT,  NAME = N'AdventureWorks2019 CON TDE', 
SKIP, NOREWIND, NOUNLOAD,  STATS = 10,COMPRESSION
GO

